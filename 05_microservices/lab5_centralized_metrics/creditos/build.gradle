buildscript {
  ext {
    springBootVersion = '1.5.10.RELEASE'
  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
    classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'org.asciidoctor.convert'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin



group = 'com.microservicios.monitoreo.creditos'
version = '0.0.1-snapshot'
sourceCompatibility = 1.8

jar {
  baseName = 'servicio-creditos'
  version = ''
}


repositories {
  mavenCentral()
}


asciidoctor {
  sourceDir 'src/main/asciidoc'
  attributes  \
       'snippets': file('build/snippets'),
          'toc': 'left'
}

ext {
  springBootAdminVersion = '1.5.7'
  springCloudVersion = 'Edgware.SR2'
}


dependencies {
  compile('org.springframework.boot:spring-boot-starter-actuator')
  compile('org.springframework.boot:spring-boot-starter-web')
  compile('org.jadira.usertype:usertype.extended:5.0.0.GA')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
  compile 'org.springframework.cloud:spring-cloud-starter-openfeign'
  runtime 'org.jolokia:jolokia-core'
  compile 'org.influxdb:influxdb-java:2.7'
  compile 'io.dropwizard.metrics:metrics-core'
  compile('org.springframework.cloud:spring-cloud-starter-eureka')

}

dependencyManagement {
  imports {
    mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}
task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
  from 'openjdk:8u151-jre-slim'
  maintainer 'jovaniac@gmail.com"'
  copyFile 'servicio-creditos.jar', '/opt/servicio-creditos.jar'

  exposePort 8082
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms256m", "-Xmx256m", '-jar', '/opt/servicio-creditos.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = 'jovaniac/servicio-creditos-observability:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tag
}

createDockerfile.dependsOn bootRepackage